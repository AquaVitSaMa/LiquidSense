allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

buildscript {
    
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()

        maven {
            name = "forge"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }

        maven {
            url = "https://plugins.gradle.org/m2"
        }

        maven {
            name = "SpongePowered"
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.10"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"

repositories {
    
    mavenLocal()
    mavenCentral()
    jcenter()
    
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

version = "Dev"
group = "me.aquavit"
archivesBaseName = "LiquidSense"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false //如果你不想让build时间变长就别开
    clientJvmArgs += ["-Dfml.coreMods.load=me.aquavit.liquidsense.injection.forge.TransformerLoader", "-Xmx1024m -Xms1024m"]
}

dependencies {
    compile("org.spongepowered:mixin:0.7.10-SNAPSHOT") {
        exclude module: "launchwrapper"
    }

    compile "com.jagrosh:DiscordIPC:0.4"

    //implementation "com.github.TheAltening:API-Java:master-SNAPSHOT"
    implementation "com.github.TheAltening:API-Java-AuthLib:-SNAPSHOT"
    compile "com.github.TheAltening:API-Java:api-2.0-SNAPSHOT"
    compile "com.github.TheAltening:API-Java-AuthLib:-SNAPSHOT"

    compile fileTree(include: ['*.jar'], dir: 'libs')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
    add sourceSets.main, "liquidsense.mixins.refmap.json"
}

jar {
    manifest.attributes(
            "MixinConfigs": "liquidsense.forge.mixins.json",
            "tweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "FMLCorePluginContainsFMLMod": "me.aquavit.liquidsense.injection.forge.MixinLoader",
            "FMLCorePlugin": "me.aquavit.liquidsense.injection.forge.TransformerLoader",
            "FMLAT": "liquidsense_at.cfg",
            "Manifest-Version": 1.0,
            "Main-Class": "me.aquavit.Instruction"
    )
}


apply plugin: "idea"

idea {
    module {
        inheritOutputDirs = true
    }
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('com.jagrosh:DiscordIPC'))
        include(dependency('org.json:json'))
        include(dependency('org.slf4j:slf4j-api'))
        include(dependency('log4j:log4j:1.2.17'))
        include(dependency('org.scijava:native-lib-loader'))
        include(dependency('com.kohlschutter.junixsocket:junixsocket-common'))
        include(dependency('com.kohlschutter.junixsocket:junixsocket-native-common'))

        include(dependency("com.github.TheAltening:API-Java"))
        include(dependency("com.github.TheAltening:API-Java-AuthLib"))
    }

    exclude 'dummyThing'
    exclude 'LICENSE.txt'

    classifier = ""
}

reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar

configurations {
    external
    compile.extendsFrom external
}

build.dependsOn shadowJar
